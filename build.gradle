/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "groovy"
    id 'com.github.ben-manes.versions' version '0.52.0'
}

version "0.1"
group = "groovy.blog"

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.apache.groovy:groovy:4.0.16")
    implementation("org.apache.groovy:groovy-macro:4.0.26")
    implementation("org.apache.groovy:groovy-ginq:4.0.16")
}

FileUtil.baseNames(sourceSets.main.allSource.files).each { name ->
    tasks.register("run$name", JavaExec) {
        dependsOn compileGroovy
        group 'Application'
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
        javaLauncher = javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(22)
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}
